import org.gradle.nativeplatform.platform.internal.Architectures
allprojects {
    version = "1.0.0"
    apply plugin: "java-library"
}

static def isX86() {
    def arch = System.getProperty("os.arch").toLowerCase()
    return Architectures.X86_64.isAlias(arch) || Architectures.X86.isAlias(arch)
}

static def isArm64() {
    def arch = System.getProperty("os.arch").toLowerCase()
    return new Architectures.KnownArchitecture("arm64", "aarch64").isAlias(arch)
}

if (isArm64() && !JavaVersion.current().is(JavaVersion.VERSION_17)) {
    throw new GradleException("Java 17 is required to build Java-Tron for arm64.\n" +
            "  Detected version ${JavaVersion.current()}")
}

if (isX86() && !JavaVersion.current().isJava8()) {
    throw new GradleException("Java 8 is required to build Java-Tron for x86.\n" +
            "  Detected version ${JavaVersion.current()}")
}

subprojects {
    apply plugin: "jacoco"
    apply plugin: "maven-publish"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.current()

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
    jacoco {
        toolVersion = "0.8.12" // see https://www.jacoco.org/jacoco/trunk/doc/changes.html
    }

    buildscript {
        repositories {
            mavenCentral()
            maven { url 'https://jitpack.io' }
            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }
        dependencies {
            classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.1'
            classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://repo.spring.io/plugins-release' }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.25'
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.13'
        implementation "com.google.code.findbugs:jsr305:3.0.0"
        implementation group: 'org.springframework', name: 'spring-context', version: '5.3.18'
        implementation group: 'org.springframework', name: 'spring-tx', version: '5.3.18'
        implementation "org.apache.commons:commons-lang3:3.4"
        implementation group: 'org.apache.commons', name: 'commons-math', version: '2.2'
        implementation "org.apache.commons:commons-collections4:4.1"
        implementation group: 'joda-time', name: 'joda-time', version: '2.3'
        implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.69'

        compileOnly 'org.projectlombok:lombok:1.18.34'
        annotationProcessor 'org.projectlombok:lombok:1.18.34'
        testCompileOnly 'org.projectlombok:lombok:1.18.34'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

        // https://www.oracle.com/java/technologies/javase/11-relnote-issues.html#JDK-8190378
        implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
        // for json-rpc, see https://github.com/briandilley/jsonrpc4j/issues/278
        implementation group: 'javax.jws', name: 'javax.jws-api', version: '1.1'
        annotationProcessor group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

        testImplementation group: 'junit', name: 'junit', version: '4.13.2'
        testImplementation "org.mockito:mockito-core:4.11.0"
        testImplementation "org.mockito:mockito-inline:4.11.0"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = "sources"
        from sourceSets.main.allSource
        duplicatesStrategy = DuplicatesStrategy.INCLUDE // allow duplicates
    }


    tasks.withType(AbstractArchiveTask) {
        preserveFileTimestamps = false
        reproducibleFileOrder = true
        duplicatesStrategy = DuplicatesStrategy.INCLUDE // allow duplicates
    }
    tasks.withType(Test).configureEach {
        // https://docs.gradle.org/current/dsl/org.gradle.api.tasks.testing.Test.html#org.gradle.api.tasks.testing.Test:environment
        environment 'CI', 'true'
    }
}

task copyToParent(type: Copy) {
    into "$buildDir/libs"
    subprojects {
        from tasks.withType(Jar)
    }
}

build.finalizedBy(copyToParent)

gradle.buildFinished {
    if (project.hasProperty('cleanSubBuild')) {
        subprojects {
            buildDir.deleteDir()
        }
    }
}
