plugins {
    id "org.sonarqube" version "2.6"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

gitProperties.failOnNoGitDirectory = false;

apply plugin: 'application'
apply plugin: 'checkstyle'

mainClassName = 'org.tron.program.FullNode'

def versions = [
        checkstyle: '8.7',
]

jacoco {
    toolVersion = "0.8.1"
}


configurations {
    checkstyleConfig

}

configurations.getByName('checkstyleConfig') {
    transitive = false
}

static def isWindows() {
    return org.gradle.internal.os.OperatingSystem.current().isWindows()
}

task version(type: Exec) {
    commandLine 'bash', '-c', '../ver.sh'
}

dependencies {
    //local libraries
    implementation fileTree(dir: 'libs', include: '*.jar')
    // end local libraries
    testImplementation group: 'org.hamcrest', name: 'hamcrest-junit', version: '1.0.0.1'
    testImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.16.0'

    implementation group: 'com.google.inject', name: 'guice', version: '4.1.0'
    implementation group: 'io.dropwizard.metrics', name: 'metrics-core', version: '3.1.2'
    implementation group: 'com.github.davidb', name: 'metrics-influxdb', version: '0.8.2'
    implementation group: 'com.carrotsearch', name: 'java-sizeof', version: '0.0.5'
    // http
    implementation 'org.eclipse.jetty:jetty-server:9.4.53.v20231009'
    implementation 'org.eclipse.jetty:jetty-servlet:9.4.53.v20231009'
    implementation 'com.alibaba:fastjson:1.2.83'
    // end http

    // https://mvnrepository.com/artifact/com.github.briandilley.jsonrpc4j/jsonrpc4j
    implementation group: 'com.github.briandilley.jsonrpc4j', name: 'jsonrpc4j', version: '1.6'

    // https://mvnrepository.com/artifact/javax.portlet/portlet-api
    compileOnly group: 'javax.portlet', name: 'portlet-api', version: '3.0.1'

    implementation "io.vavr:vavr:0.9.2"
    implementation group: 'org.pf4j', name: 'pf4j', version: '2.5.0'

    testImplementation group: 'org.springframework', name: 'spring-test', version: '5.2.0.RELEASE'
    testImplementation group: 'org.springframework', name: 'spring-web', version: '5.2.0.RELEASE'

    implementation group: 'org.zeromq', name: 'jeromq', version: '0.5.3'
    api project(":actuator")
    api project(":consensus")
}

check.dependsOn 'lint'

checkstyle {
    toolVersion = "${versions.checkstyle}"
    configFile = file("config/checkstyle/checkStyleAll.xml")
}


checkstyleMain {
    source = 'src/main/java'
}

task lint(type: Checkstyle) {
    // Cleaning the old log because of the creation of the new ones (not sure if totaly needed)
    delete fileTree(dir: "${project.rootDir}/app/build/reports")
    source 'src'
    include '**/*.java'
    exclude 'main/gen/**'
    exclude 'test/**'
    // empty classpath
    classpath = files()
    //Failing the build
    ignoreFailures = false
}

tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}

if (project.hasProperty("mainClass")) {
    mainClassName = mainClass
}

run {
    if (project.hasProperty('witness')) {
        standardInput = System.in
        args = ['--witness']
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
    if (isWindows()) {
        exclude '**/ShieldedTransferActuatorTest.class'
        exclude '**/BackupDbUtilTest.class'
        exclude '**/ManagerTest.class'
        exclude 'org/tron/core/zksnark/**'
        exclude 'org/tron/common/runtime/vm/PrecompiledContractsVerifyProofTest.class'
        exclude 'org/tron/core/ShieldedTRC20BuilderTest.class'
        exclude 'org/tron/common/runtime/vm/WithdrawRewardTest.class'
    }
    maxHeapSize = "1024m"
    doFirst {
        forkEvery = 100
        jvmArgs "-XX:MetaspaceSize=128m","-XX:MaxMetaspaceSize=256m", "-XX:+UseG1GC"
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
    getExecutionData().setFrom(fileTree('../framework/build/jacoco').include("**.exec"))
}

def binaryRelease(taskName, jarName, mainClass) {
    return tasks.create("${taskName}", Jar) {
        baseName = jarName
        version = null
        from(sourceSets.main.output) {
            include "/**"
        }
        // explicit_dependency
        dependsOn (project(':actuator').jar, project(':consensus').jar, project(':chainbase').jar,
                project(':crypto').jar, project(':common').jar, project(':protocol').jar)
        from {
            configurations.runtimeClasspath.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }

        // exclude these files for bouncycastle
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"

        manifest {
            attributes "Main-Class": "${mainClass}"
        }

//        classifier(jarName)
    }
}

def createScript(project, mainClass, name) {
    project.tasks.create(name: name, type: CreateStartScripts) {
        unixStartScriptGenerator.template = resources.text.fromFile('../gradle/unixStartScript.txt')
        windowsStartScriptGenerator.template = resources.text.fromFile('../gradle/windowsStartScript.txt')
        outputDir = new File(project.buildDir, 'scripts')
        mainClassName = mainClass
        applicationName = name
        classpath = project.tasks[JavaPlugin.JAR_TASK_NAME].outputs.files + project.configurations.runtimeClasspath
//        defaultJvmOpts = ['-XX:+UseConcMarkSweepGC',
//                          '-XX:+PrintGCDetails',
//                          '-Xloggc:./gc.log',
//                          '-XX:+PrintGCDateStamps',
//                          '-XX:+CMSParallelRemarkEnabled',
//                          '-XX:ReservedCodeCacheSize=256m',
//                          '-XX:+CMSScavengeBeforeRemark'
//        ]
    }
    project.tasks[name].dependsOn(project.jar)
    project.applicationDistribution.with {
        into("bin") {
            from(project.tasks[name])
            fileMode = 0755
        }
    }
}

applicationDistribution.from("../gradle/java-tron.vmoptions") {
    into "bin"
}
//distZip {
//    doLast {
//        file("$destinationDir/$archiveName").renameTo("$destinationDir/"+'java-tron-'+version+'-bin.zip')
//    }
//}
configurations.archives.artifacts.removeAll { it.type == 'tar' }
applicationName = 'java-tron'
startScripts.enabled = false
run.enabled = false
tasks.distTar.enabled = false

createScript(project, 'org.tron.program.SolidityNode', 'SolidityNode')
createScript(project, 'org.tron.program.FullNode', 'FullNode')
createScript(project, 'org.tron.program.KeystoreFactory', 'KeystoreFactory')
createScript(project, 'org.tron.program.DBConvert', 'DBConvert')

def releaseBinary = hasProperty('binaryRelease') ? getProperty('binaryRelease') : 'true'
def skipSolidity = hasProperty('skipSolidity') ? true : false
def skipKeystore = hasProperty('skipKeystore') ? true : false
def skipConvert = hasProperty('skipConvert') ? true : false
def skipAll = hasProperty('skipAll') ? true : false
if (releaseBinary == 'true') {
    artifacts {
        archives(binaryRelease('buildFullNodeJar', 'FullNode', 'org.tron.program.FullNode'))
    }
    if (!skipAll) {
        if (!skipSolidity) {
            artifacts {
                archives(binaryRelease('buildSolidityNodeJar', 'SolidityNode', 'org.tron.program.SolidityNode'))}
        }
        if (!skipKeystore) {
            artifacts {
                archives(binaryRelease('buildKeystoreFactoryJar', 'KeystoreFactory', 'org.tron.program.KeystoreFactory'))}
        }
        if (!skipConvert) {
            artifacts {
                archives(binaryRelease('buildDBConvertJar', 'DBConvert', 'org.tron.program.DBConvert'))}
        }
    }

}

task copyToParent(type: Copy) {
    into "../build/distributions"
    from "$buildDir/distributions"
    include "*.zip"
}

build.finalizedBy(copyToParent)
